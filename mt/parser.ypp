%skeleton "lalr1.cc"
%require  "3.3"
%defines
%define api.namespace {mt}
%define api.parser.class {Parser}
%define parse.error verbose

%code requires{
  namespace Support {
      class Messages;
  }
  namespace mt {
      class Driver;
      class Scanner;
   }
}

%parse-param { Support::Messages& errs  }
%parse-param { Scanner* scanner }
%parse-param { Driver& driver  }

%code{
   #include <iostream>
   #include <cstdlib>
   #include <fstream>
   #include "location.hh"
   #include "mt/Driver.h"
   #include "mt/Scanner.h"
   #include "support/Message.h"

#undef yylex
#define yylex scanner->yylex

}

%define api.value.type variant
%define parse.assert

%token               END    0     "end of file"
%token PARM
%token <std::string> TEXT
%token <std::string> WSS
%token <std::string> MACRO_IN
%token <std::string> MACRO_OUT
%token <std::string> INJECTION

%locations

%%

list_option : END | list END;

list
  : item
  | list item
  ;

item
  : TEXT			{
		driver.store($1, yyla.location);
	}
  | WSS		      { driver.storeWss($1, yyla.location); }
  | INJECTION		{ driver.inject($1, yyla.location); }
  | macro			  { driver.store_macro(); }
  ;

macro
: MACRO_IN		{ driver.new_macro($1, yyla.location); }
    parms
    MACRO_OUT		{ driver.store($4, yyla.location); driver.add_parm(); }
  ;

parms
  : %empty
  | parm
  | parms parm
  ;

parm
  : pitem
  | parm pitem

pitem
  : PARM			{ driver.add_parm(); }
  | WSS		        { driver.storeWss($1, yyla.location); }
  | TEXT			{ driver.store($1, yyla.location); }
  | INJECTION		{ driver.inject($1, yyla.location); }
  | macro			{ driver.store_macro(); }
  ;

%%
void mt::Parser::error( const location_type &loc, const std::string &err_message ) {
	std::string data(err_message);
	if(data.find("syntax error, ") == 0) {
		data.erase(0, 14);
	}
	size_t pos = data.find("file");
	while (pos != std::string::npos) {
		data.replace(pos, 4, "text");
		pos = data.find("file", pos + 4);
	}
	errs << Message(fatal,data,loc);
}
